plugins {
	id 'org.springframework.boot' version '2.3.1.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id "com.github.erdi.webdriver-binaries" version "2.2"
	id 'groovy'
}

group 'com.makingdevs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.codehaus.groovy:groovy-all:2.5.12'
	implementation 'mysql:mysql-connector-java:8.0.20'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		//exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
	testImplementation "org.gebish:geb-spock:3.4"
	testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:3.141.59"
	testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:3.141.59"
}

test {
	useJUnitPlatform()
}

webdriverBinaries {
	chromedriver {
		version = '83.0.4103.39'
		fallbackTo32Bit = true
	}
	geckodriver {
		version = '0.26.0'
	}
}

["chrome", "firefox"].each { driver ->
	task "${driver}Test"(type: Test) {
		group JavaBasePlugin.VERIFICATION_GROUP

			outputs.upToDateWhen { false }  // Always run tests

		systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
		systemProperty "geb.env", driver
	}
}
